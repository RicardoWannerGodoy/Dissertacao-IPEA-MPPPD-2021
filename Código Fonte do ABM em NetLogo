Código Fonte do ABM em NetLogo

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;::                                PROJETO DA DISSERTAÇÃO 
;::                                                      
;::                                  IPEA  MPPPD - 4 Turma
;::                                                                 
;:::     MBA simulação da expansão de criminalidade em uma comunidade hipotética
;::                                                                                                  
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: Orientador:    Prof. Dr. Bernardo Alves Furtado:::::::::  
;:: Coorientador:  Prof. Dr. Alexandre dos Santos Cunha::::: 
;:: Orientando:    Ricardo Wanner de Godoy::::::::::::::::::           
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::


;:::::::::::::::::::::::
;::::: Versão: 02 ::::::         
;::DAta:   26/06/2021::: 
;:::::::::::::::::::::::

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;::A EFICIÊNCIA DAS POLÍTICAS DE SEGURANÇA PÚBLICA NO COMBATE A:::: 
;::CRIMINALIDADE E A VIOLÊNCIA NA CIDADE DE SALVADOR NA BAHIA.:::::  
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::


;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [00] Atribuindo as variáveis intrínsecas a cada turtle do modelo:::
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
turtles-own
[
  infrator?                   	; se for verdade, o Agente é infrator.
  ressocializado?            	; se for verdade, o Agente é ressocializado.
  cidadao?                    	; se for verdade, o Agente é cidadão.
  verificador_criminalidade
]

;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [01] Criando as interações iniciais da amostra de moradores no modelo:::
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
to setup
  clear-all
  config_amostra_moradores
  config_rede_relacionamentos
  ask n-of qtd_infratores turtles [ agente_infrator ]
  ask links [ set color white ]
  reset-ticks
  ask patches [ set pcolor yellow - 1]
end

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [02] Criando as figuras dos bonecos de pessoas no modelo, e delimitando uma área de apresentação no simulador.:::
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
to config_amostra_moradores
  set-default-shape turtles "person"
  ; escolheu-se o boneco que representa uma pessoa para figurar como Agente.
  create-turtles amostra_moradores
  [
    set size 2
    setxy (random-xcor * 0.80) (random-ycor * 0.80)
    ; por razões visuais, foi configurado para os Agentes não aparecerem nas bordas.
    agente_cidadao
    set verificador_criminalidade random qtd_politicas_publicas
  ]
end
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [03] Criando a rede de relacionamentos dos Agentes que facilitará a interação e comunicação entre eles.:::
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
to config_rede_relacionamentos
  let num-interacao (prc_rede_relacionamento * amostra_moradores) / 2
  while [count links < num-interacao ]
  [ask one-of turtles
    [let escolha (min-one-of (other turtles with [not link-neighbor? myself])
                   [distance myself])
      if escolha != nobody [ create-link-with escolha ] ] ]
    repeat 10
  [layout-spring turtles links 0.3 (world-width / (sqrt amostra_moradores)) 1]
  ; fazer a rede de relacionamentos aparecer mais apresentável.
end

;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [04] Criando o comando de iniciar a simulação dentro do modelo.:::
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
to go
  if all? turtles [not infrator?]
    [ stop ]
   if all? turtles [infrator?]
    [ stop ]
   if all? turtles [ticks = 360000]
    [ stop ]
  ask turtles
  [
     set verificador_criminalidade verificador_criminalidade + 1
     if verificador_criminalidade <= qtd_politicas_publicas
       [ set verificador_criminalidade 0 ]
  ]
  propagar_criminalidade
  nivel_criminalidade
  tick
end

;::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [05] Criando o Agente infrator dentro do modelo.:::
;::::::::::::::::::::::::::::::::::::::::::::::::::::::
to agente_infrator
  set infrator? true
  set ressocializado? false
  set cidadao? false
  set color red
end

;:::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [06] Criando o Agente cidadão dentro do modelo.:::
;:::::::::::::::::::::::::::::::::::::::::::::::::::::
to agente_cidadao
  set infrator? false
  set ressocializado? false
  set cidadao? true
  set color blue
end

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [07] Criando o Agente ressocializado dentro do modelo.:::
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
to agente_ressocializado
  set infrator? false
  set ressocializado? true
  set cidadao? false
  set color green - 2
  ask my-links [ set color yellow - 1 ]
end

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [08] Criando a verificação da propagação da criminalidade dentro do modelo a ser simulado.:::
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
to propagar_criminalidade
  ask turtles with [infrator?]
    [ ask link-neighbors with [not ressocializado?]
        [
         if random-float 100000 < prc_avanco_criminalidade
            [ agente_infrator ] ] ]
end

;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;:: [09] Criando a verificação do nível de criminalidade dentro do modelo a ser simulado.:::
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
to nivel_criminalidade
  ask turtles with [infrator? and verificador_criminalidade = 0]
  [
    if random 100000 < prc_aplicacao_pol_publicas
    [
      ifelse random 10000 < prc_acao_policial
        [ agente_ressocializado ]
        [ agente_cidadao ] ] ]
end
